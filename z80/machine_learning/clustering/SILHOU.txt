"SILHOUETTE COEFFICIENT"
ClrHome
Input "NUM CLUSTERS?",C

"NUM OF PTS PER CLUSTER"
SetUpEditor ʟPTS
C→dim(ʟPTS)

For(I,1,C,1)
  ClrHome
  Disp "NUM PTS IN C",I
  Input "P?",P
  P→ʟPTS(I)
End

"CLUSTERS"
"X VALS"
{C,max(ʟPTS)}→dim([A])
"Y VALS"
{C,max(ʟPTS)}→dim([B])

"GETS PTS IN EACH CLUSTERS"
For(I,1,C,1)
  ClrHome
  Output(1,1,"ENTER PTS")
  Output(2,1,"FOR C=")
  Output(2,7,I)
  Output(3,1,"TOTAL PTS")
  Output(4,1,"=")
  Output(4,2,ʟPTS(I))
  Pause 
  
  For(J,1,ʟPTS(I),1)
    ClrHome
    Disp "CLUSTER",I
    Disp "PT",J
    Prompt X
    Prompt Y
    X→[A](I,J)
    Y→[B](I,J)
  End
End

"CALC SILHOUETTE COEFFICIENT"
0→Z
For(I,1,C,1)
  "FOR EACH CLUSTER"
  For(J,1,ʟPTS(I),1)
    "FOR EACH PT IN THE CLUSTER"
    "AVG INTRA DIST"
    0→A
    
    For(K,1,ʟPTS(I),1)
      "CALC TOTAL INTRA DIST"
      If K≠J:Then
        A+√(([A](I,J)-[A](I,K))^2+([B](I,J)-[B](I,K))^2)→A
      
        "DISPLAY DIST FROM PT"
        ClrHome
        Output(1,1,"C=")
        Output(1,3,I)
        Output(2,1,"PT=")
        Output(2,4,J)
        Output(3,1,"X=")
        Output(3,3,[A](I,J))
        Output(4,1,"Y=")
        Output(4,3,[B](I,J))
        Output(6,1,"PT=")
        Output(6,4,K)
        Output(7,1,"X=")
        Output(7,3,[A](I,K))
        Output(8,1,"Y=")
        Output(8,3,[B](I,K))
        Pause 
        ClrHome
        Output(1,1,"DIST")
        Output(2,1,"=")
        Output(2,2,√(([A](I,J)-[A](I,K))^2+([B](I,J)-[B](I,K))^2))
        Pause 
      End
    End
    
    "DISPLAY AVG INTRA DIST"
    ClrHome
    Output(1,1,"C=")
    Output(1,3,I)
    Output(2,1,"AVG INTRA DIST")
    Output(3,1,"=")
    Output(3,2,A)
    Output(4,1,"/")
    Output(4,2,ʟPTS(I)-1)
    
    "CALC AVG INTRA DIST"
    A/(ʟPTS(I)-1)→A

    Output(5,1,"=")
    Output(5,2,A)
    Pause 
    
    "MIN AVG INTER DIST"
    1ᴇ99→B
    "CALC INTER DIST"
    For(K,1,C,1)
      0→D
      If K≠I:Then
        "CALC TOTAL INTER DIST"
        For(L,1,ʟPTS(K),1)
         D+√(([A](I,J)-[A](K,L))^2+([B](I,J)-[B](K,L))^2)→D
          
          "DISPLAY DIST"
          ClrHome
          Output(1,1,"C=")
          Output(1,3,I)
          Output(2,1,"PT=")
          Output(2,4,J)
          Output(3,1,"X=")
          Output(3,3,[A](I,J))
          Output(4,1,"Y=")
          Output(4,3,[B](I,J))
          Output(5,1,"C=")
          Output(5,3,K)
          Output(6,1,"PT=")
          Output(6,4,L)
          Output(7,1,"X=")
          Output(7,3,[A](K,L))
          Output(8,1,"Y=")
          Output(8,3,[B](K,L))
          Pause 
          ClrHome
          Output(1,1,"DIST")
          Output(2,1,"=")
          Output(2,2,√(([A](I,J)-[A](K,L))^2+([B](I,J)-[B](K,L))^2))
          Pause 
        End
        
        "DISPLAY AVG INTER DIST"
        ClrHome
        Output(1,1,"AVG INTER DIST")
        Output(2,1,"C=")
        Output(2,3,I)
        Output(3,1,"PT=")
        Output(3,4,J)
        Output(4,1,"X=")
        Output(4,3,[A](I,J))
        Output(5,1,"Y=")
        Output(5,3,[B](I,J))
        Output(7,1,"C=")
        Output(7,3,K)
        Pause 
        ClrHome
        Output(1,1,"AVG INTER DIST")
        Output(2,1,"D=")
        Output(2,3,D)
        Output(3,1,"/")
        Output(3,2,ʟPTS(K))
        
        "CALC AVG INTER DIST"
        D/(ʟPTS(K))→D
        
        Output(4,1,"=")
        Output(4,2,D)
        Pause 
        
        If D<B:Then
          D→B
        End
      End
    End
    
    "DISPLAY MIN AVG INTER DIST"
    ClrHome
    Output(1,1,"MIN AVG")
    Output(2,1,"INTER DIST")
    Output(3,1,"B=")
    Output(3,3,B)
    Pause 
    
    "SILHOUETTE COEFFICIENT OF PT"
    (B-A)/(max(A,B))→S
    "TOTAL SIL COEF"
    Z+S→Z
    
    "DISPLAY SILHOU COEF"
    ClrHome
    Output(1,1,"C=")
    Output(1,3,I)
    Output(2,1,"PT=")
    Output(2,4,J)
    Output(3,1,"X=")
    Output(3,3,[A](I,J))
    Output(4,1,"Y=")
    Output(4,3,[B](I,J))
    Output(5,1,"SILO COEF")
    Output(6,1,"S=")
    Output(6,3,S)
    Pause 
  End
End

ClrHome
Output(1,1,"AVG SILO COEF")
Output(2,1,"=")
Output(2,3,Z)
Output(3,1,"/")
Output(3,2,sum(ʟPTS))

Z/sum(ʟPTS)→Z

Output(4,1,"=")
Output(4,2,Z)
Pause 

DelVar ʟPTS
ClrHome